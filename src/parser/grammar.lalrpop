use crate::parser::{lexer, ast, Error};
use lexer::Tok;
use ast::Ast;

grammar();

pub Ast: ast::Ast = {
    #[precedence(level="0")]
    Term,
    
    #[precedence(level="1")] #[assoc(side="right")]
    <t: Ast> "?" => Ast::optional(t),
    <t: Ast> "*" => Ast::zero_or_more(t),
    <t: Ast> "+" => Ast::one_or_more(t),
    
    #[precedence(level="2")] #[assoc(side="left")]
    <lhs: Ast> <rhs: Ast> => Ast::then(lhs, rhs),
    
    #[precedence(level="3")] #[assoc(side="left")]
    <lhs: Ast> "|" <rhs: Ast> => Ast::or(lhs, rhs),
}

Term: ast::Ast = {
    <c: "char"> => Ast::char(c),
    <"digit"> => Ast::digit(),
    <" "> => Ast::space(),
    "(" <p: Ast> ")" => p,
    "(" "!" <p: Ast> ")" => Ast::case_sensitive(p)
}

extern {
    type Location = usize;
    type Error = Error;

    enum Tok {
        "|" => lexer::Tok::Or,
        "?" => lexer::Tok::QMark,
        "*" => lexer::Tok::Star,
        "+" => lexer::Tok::Plus,
        "(" => lexer::Tok::LParen,
        ")" => lexer::Tok::RParen,
        "!" => lexer::Tok::Exclam,
        "char" => lexer::Tok::Char { c: <char>, .. },
        "digit" => lexer::Tok::Digit,
        " " => lexer::Tok::Space,
    }
}